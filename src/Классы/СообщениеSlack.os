#Использовать json

Перем text;
Перем username;
Перем icon_url;
Перем icon_emoji;
Перем channel;
Перем attachments;

Процедура ПриСозданииОбъекта(Знач Текст = "", Знач ИмяПользователя = "", Знач УРЛЗначка = "", Знач ЭмодзиЗначка = "", Знач Канал = "")
	
	text = Текст;
	username = ИмяПользователя;
	icon_url = УРЛЗначка;
	icon_emoji = ЭмодзиЗначка;
	channel = Канал;
	attachments = Новый Массив;
	
КонецПроцедуры

#Область ПрограммныйИнтерфейс

Функция ДобавитьВложение(Вложение) Экспорт
	
	attachments.Добавить(Вложение);
	
	Возврат ЭтотОбъект;
	
КонецФункции

Функция Текст(Знач Значение) Экспорт
	text = Значение;
	Возврат ЭтотОбъект;
КонецФункции

Функция ИмяПользователя(Знач Значение) Экспорт
	username = Значение;
	Возврат ЭтотОбъект;
КонецФункции

Функция УРЛЗначка(Знач Значение) Экспорт
	icon_url = Значение;
	Возврат ЭтотОбъект;
КонецФункции

Функция ЭмодзиЗначка(Знач Значение) Экспорт
	icon_emoji = Значение;
	Возврат ЭтотОбъект;
КонецФункции

Функция Канал(Знач Значение) Экспорт
	channel = Значение;
	Возврат ЭтотОбъект;
КонецФункции

Функция КонвертироватьВJSON() Экспорт
	Парсер = Новый ПарсерJSON;

	СтруктураСообщения = Новый Структура;
	СтруктураСообщения.Вставить("text", text);
	СтруктураСообщения.Вставить("username", username);
	СтруктураСообщения.Вставить("icon_url", icon_url);
	СтруктураСообщения.Вставить("icon_emoji", icon_emoji);
	СтруктураСообщения.Вставить("channel", channel);
	СтруктураСообщения.Вставить("attachments", Новый Массив);
	Для каждого Вложение Из attachments Цикл
		СтруктураСообщения.attachments.Добавить(Вложение.ВСтруктуру());
	КонецЦикла;
	
	Возврат Парсер.ЗаписатьJSON(СтруктураСообщения);
КонецФункции

#КонецОбласти
